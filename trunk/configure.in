AC_INIT([moc],[DEVEL],[daper@daper.net])
AC_CONFIG_SRCDIR([main.c])
AC_CONFIG_HEADERS([config.h])

AM_INIT_AUTOMAKE

AC_PREREQ(2.59)

AC_CANONICAL_HOST
AC_PROG_CC
AC_PROG_INSTALL

AC_PROG_AWK
AC_LIBTOOL_DLOPEN
AC_DISABLE_STATIC
AC_ENABLE_SHARED
AC_PROG_LIBTOOL
AC_LIB_LTDL

AC_SUBST([EXTRA_OBJS])
AC_SUBST([DECODER_PLUGINS])

plugindir=$libdir/moc
AC_SUBST([plugindir])
DECODER_PLUGIN_DIR=decoder_plugins
AC_SUBST([DECODER_PLUGIN_DIR])

PLUGIN_LDFLAGS='-module -avoid-version'
AC_SUBST([PLUGIN_LDFLAGS])

LDFLAGS="$LDFLAGS -export-dynamic"

AC_HEADER_STDC
AC_CHECK_HEADERS([fcntl.h string.h sys/param.h unistd.h sys/un.h \
		  sys/socket.h sys/types.h signal.h sys/time.h \
		  sys/wait.h sys/ioctl.h \
		  time.h errno.h sys/stat.h assert.h],,
		 AC_MSG_ERROR([Can't find required header files.]))
AC_CHECK_HEADERS([sys/select.h])

AC_C_BIGENDIAN

dnl iconv()
AC_CHECK_LIB([iconv], [iconv_open], [EXTRA_LIBS="$EXTRA_LIBS -liconv"])
AC_CHECK_HEADERS([iconv.h],
		 [AC_DEFINE([HAVE_ICONV], 1, [Define if you have iconv.h])
		 COMPILE_ICONV=yes],
		 [COMPILE_ICONV=no])

AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AX_CFLAGS_GCC_OPTION(-Wall)
AX_CFLAGS_GCC_OPTION(-W)

COMPILE_OSS="no"
AC_CHECK_HEADER([sys/soundcard.h],
		[AC_DEFINE([HAVE_OSS], 1, [Define if you have OSS.])
		EXTRA_OBJS="$EXTRA_OBJS oss.o"
		COMPILE_OSS="yes"],
		[echo -e "\nWARNING!!! YOU DONT HAVE OSS, THE PROGRAM CAN BE USELESS!\n"])
if test "$COMPILE_OSS" = yes
then
	AC_CHECK_LIB([ossaudio], [_oss_ioctl],
		     [EXTRA_LIBS="$EXTRA_LIBS -lossaudio"])
fi

PKG_CHECK_MODULES(ALSA, [alsa >= 0.9],
	     [COMPILE_ALSA="yes"
	      EXTRA_OBJS="$EXTRA_OBJS alsa.o"
	      AC_DEFINE([HAVE_ALSA], 1, [Define if you have ALSA.])
	      EXTRA_LIBS="$EXTRA_LIBA $ALSA_LIBS"
	      CFLAGS="$CFLAGS $ALSA_CFLAGS"],
	     [COMPILE_ALSA="no"])

AC_ARG_ENABLE(debug, AS_HELP_STRING(--disable-debug,Disable debug code))

if test "x$enable_debug" = "xno"
then
	AC_DEFINE([NDEBUG], 1, [Define if you don't want debug code])
	COMPILE_DEBUG='no'
else
	AX_CFLAGS_GCC_OPTION(-g)
	COMPILE_DEBUG='yes'
	EXTRA_OBJS="$EXTRA_OBJS null_out.o"
fi


AC_FUNC_MALLOC
AC_FUNC_STAT
AC_CHECK_FUNCS([getcwd memmove strcasecmp strdup strerror strncasecmp strrchr \
		socket gettimeofday getenv fork setsid kill bind listen \
		accept time connect unlink send recv select strftime \
		localtime_r strerror_r],,
	       AC_MSG_ERROR([Required standard C functions are not present.]))
AC_CHECK_FUNCS([strcasestr])

dnl FIXME: Support for this in the source code
AC_HEADER_DIRENT

ACX_PTHREAD 
AC_FUNC_MMAP

if test "$acx_pthread_ok" != "yes"
then
	AC_MSG_ERROR([[I don't know how to compile pthread code on this system]])
fi

CC="$PTHREAD_CC"
CFLAGS="$PTHREAD_CFLAGS $CFLAGS"
EXTRA_LIBS="$EXTRA_LIBS $PTHREAD_LIBS"

dnl __FUNCTION__
AC_TRY_COMPILE(,[printf(__FUNCTION__);], [AC_DEFINE([HAVE__FUNCTION__], 1,
	       [Define if we have __FUNCTION__ constant])])

dnl __attribute__
AC_TRY_COMPILE([#include <stdarg.h>],
	       [int f(const char *f, ...)
	       __attribute__((format (printf, 1, 2)))],
	       [AC_DEFINE([HAVE__ATTRIBUTE__], 1,
			  [Define if we have __attribute__ extension])])

dnl ncurses
MP_WITH_CURSES
if test -z "$CURSES_LIB"
then
		AC_MSG_ERROR([You need curses/ncurses library and header files.])
fi

dnl getopt
AC_CHECK_FUNC(getopt_long,
	      [AC_CHECK_HEADERS([getopt.h],,[AC_MSG_ERROR([You need getopt.h])])],
	      [#FreeBSD has it there
	       AC_CHECK_LIB(gnugetopt,getopt_long,,
			    [AC_LIBOBJ(getopt)
			    AC_LIBOBJ(getopt1)
			    AC_CONFIG_LINKS([getopt.h:gnugetopt.h])]
			    )]
	      )
	      
dnl libmad (mp3)
AC_ARG_WITH(mp3, AS_HELP_STRING(--without-mp3,Compile without mp3 support (libmad)))
if test "x$with_mp3" != "xno"
then
	AC_CHECK_LIB(mad, mad_stream_init, [
		AC_CHECK_HEADER([mad.h], ,
			AC_MSG_ERROR([You need mad header file (mad devel package).]))
	])

	if test "$ac_cv_lib_mad_mad_stream_init" = yes
	then
		dnl libid3tag (with zlib)
		AC_CHECK_LIB(z, gzopen, [true],
			     [AC_MSG_ERROR([You need zlib for libid3tag to play mp3, check the README file.])])
		AC_CHECK_LIB(id3tag, id3_file_open, [true],
			     [AC_MSG_ERROR([You need libid3tag to play mp3, check the README file.])],
			     [-lz])
		AC_CHECK_HEADER([id3tag.h],,
				[AC_MSG_ERROR([You need header file for libid3tag to compile with mp3 support, check the README file.])])
		DECODER_PLUGINS="$DECODER_PLUGINS mp3"
	fi
fi

dnl vorbis
AC_ARG_WITH(ogg, AS_HELP_STRING(--without-ogg,Compile without OGG support.))
if test "x$with_ogg" != "xno"
then
	PKG_CHECK_MODULES(OGG_VORBIS, [ogg >= 1.0 vorbis >= 1.0 vorbisfile >= 1.0],
			  [AC_SUBST(OGG_VORBIS_LIBS)
			   DECODER_PLUGINS="$DECODER_PLUGINS ogg"])
fi

dnl FLAC
AC_ARG_WITH(flac, AS_HELP_STRING(--without-flac,Compile without FLAC support.))
if test "x$with_flac" != "xno"
then
	AM_PATH_LIBFLAC([AC_SUBST(LIBFLAC_LIBS)
		AC_SUBST(LIBFLAC_CFLAGS)
		DECODER_PLUGINS="$DECODER_PLUGINS flac"])
fi

dnl libsndfile
AC_ARG_WITH(sndfile, AS_HELP_STRING(--without-sndfile,
	    Compile without libsndfile))
if test "x$with_sndfile" != "xno"
then
	PKG_CHECK_MODULES(sndfile, sndfile >= 1.0.0,
			   [AC_SUBST(sndfile_LIBS)
			   AC_SUBST(sndfile_CFLAGS)
			   DECODER_PLUGINS="$DECODER_PLUGINS sndfile"],
			   [true])
fi

dnl curl
COMPILE_CURL="no"
AC_ARG_WITH(curl, AS_HELP_STRING(--without-curl,
	    [Compile without curl (Internet streams support)]))
if test "x$with_curl" != "xno"
then
	dnl FIXME: make a better curl test
	AC_CHECK_PROG([CURL_CONFIG], [curl-config], [yes])
	if test "x$CURL_CONFIG" = "xyes"
	then
		CFLAGS="$CFLAGS `curl-config --cflags`"
		EXTRA_LIBS="$EXTRA_LIBS `curl-config --libs`"
		AC_DEFINE([HAVE_CURL], 1, [Define if you have libcurl])
		COMPILE_CURL="yes"
	fi
fi

EXTRA_LIBS="$EXTRA_LIBS $CURSES_LIB"
AC_SUBST(EXTRA_LIBS)
		
AC_OUTPUT([Makefile
	  themes/Makefile
	  decoder_plugins/Makefile
	  decoder_plugins/mp3/Makefile
	  decoder_plugins/ogg/Makefile
	  decoder_plugins/flac/Makefile
	  decoder_plugins/sndfile/Makefile
	  ])

echo
echo "----------------------------------------------------------------"
echo "MOC will be compiled with:"
echo "Decoder plugins:   "$DECODER_PLUGINS
echo "OSS:               "$COMPILE_OSS
echo "ALSA:              "$COMPILE_ALSA
echo "DEBUG:             "$COMPILE_DEBUG
echo "ICONV:             "$COMPILE_ICONV
echo "Network streams    "$COMPILE_CURL
echo
echo "----------------------------------------------------------------"

echo "WARNING: since MOC version 2 the executable file name was changed to mocp!"
echo "Please remove old moc binary if you have installed an older version."
